load "$NCARG_ROOT/lib/ncarg/nclscripts/csm/contributed.ncl"
f    = addfile("toc_jan.nc","r")                 ; read in january ozone data
toc=f->field34172(:,:,:)
date= f->time_1(:)
lat = toc&lat

selectedday= (/460,566,916,967,1071,1087,1136,1172,1218,1317,1453,1480,1526,1556/) ; timestamps for low ozone events

gooddate= cd_calendar(date,0)
TIME = gooddate 
year    = toint( TIME(:,0) )   
month   = toint( TIME(:,1) )   
day     = toint( TIME(:,2) )    
ddd     = day_of_year(year, month, day)    
yyyyddd = year*1000 + ddd       ;print(yyyyddd)

tocclm = clmDayTLL(toc,yyyyddd)                   ; daily climatology
tocanom = calcDayAnomTLL(toc,yyyyddd,tocclm)       ; anomalies
printVarSummary(tocanom) 

average = dim_avg_n_Wrap(tocanom(selectedday,:,:),0)     ; average anomaly for low ozone events
printVarSummary(average)

x = tocanom                           ; to add stippling
y = tocanom(selectedday,:,:)
dimXY = dimsizes(x)
ntim  = dimXY(0)
nlat  = dimXY(1)
mlon  = dimXY(2)                                  

xtmp = x(lat|:,lon|:,time_1|:)       ; reorder but do it only once [temporary]
ytmp = y(lat|:,lon|:,time_1|:)                                      
xAve = dim_avg_Wrap (xtmp)              ; calculate means at each grid point 
yAve = dim_avg_Wrap (ytmp)
xVar = dim_variance (xtmp)         ; calculate variances
yVar = dim_variance (ytmp)                                

sigr = 0.05                        ; critical sig lvl for r
xEqv = equiv_sample_size (xtmp, sigr,0)
yEqv = equiv_sample_size (ytmp, sigr,0)

wgt  = latRegWgt(lat, "float", 0)
printVarSummary(wgt)

wgty = wgt

xN   = wgt_areaave (xEqv, wgty, 1., 0)    ; wgty could be gaussian weights 
yN   = wgt_areaave (yEqv, wgty, 1., 0)
print(xN)
print(yN)                               

iflag= False                        ; population variance similar
prob = xAve
prob = ttest(xAve,xVar,xN,yAve,yVar,yN,iflag,False) 
printVarSummary(prob)
printMinMax(prob,True)

wks_type = "png"
wks_type@wkWidth = 2000
wks_type@wkHeight = 2000
wks = gsn_open_wks(wks_type,"tocanomavg")                ; send graphics to PNG file    

res                      = True                  ; plot mods desired
res@tiMainString         = "Average Ozone Anomalies"     ; title
res@gsnMaximize = True
res@cnFillOn             = True                  ; turn on color fill
res@cnFillPalette        = "BlWhRe"              ; choose colormap
res@cnLinesOn            = False
res@gsnLeftString   = ""              ; add the gsn titles
res@gsnCenterString = ""
res@gsnRightString  = ""
res@cnLevelSelectionMode = "ManualLevels"        ; set manual contour levels
res@cnMinLevelValF       =  -32.5               ; set min contour level
res@cnMaxLevelValF       =   32.5                 ; set max contour level
res@cnLevelSpacingF      =   5             ; set contour spacing
res@mpGeophysicalLineThicknessF = 2
res@mpLimitMode           = "LatLon"
res@mpMinLatF             =   -60.
res@mpMaxLatF             =    30.
res@mpMinLonF             =   100.
res@mpMaxLonF             =   180. 

res3 = True
res3@cnLinesOn = False
res3@cnLineLabelsOn = False
res3@cnLevelSelectionMode = "ExplicitLevels"
res3@cnLevels = (/0.,0.05/)
res3@cnFillPatterns = (/-1,16,-1/)
res3@cnInfoLabelOn = False
res3@tiMainOn = False
res3@cnFillOn = True
res3@cnMonoFillPattern = False
res3@cnFillColor = "black"
res3@cnMonoFillColor = True
res3@cnFillScaleF        = .5        ; add extra density

plot = gsn_csm_contour_map_overlay(wks,average(:,:),\prob(:,:),res,res3)        
